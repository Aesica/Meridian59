% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
QormasGift is PassiveItem

constants:

   include blakston.khd

resources:

   QormasGift_icon_rsc = gift1.bgf
   QormasGift_icon_rsc1 = gift1.bgf
   QormasGift_icon_rsc2 = gift2.bgf
   QormasGift_icon_rsc3 = gift3.bgf
   QormasGift_icon_rsc4 = gift4.bgf
   QormasGift_name_rsc = "Qormas Present"
   QormasGift_desc_rsc = \
      "This gift is intricately wrapped in the finest paper available in the"
      " land."
   QormasGift_desc_append1 = " The tag reads:\n\nTo: "
   QormasGift_desc_append2 = "\nFrom: Qor The Vile"

classvars:
   
   vrName = QormasGift_name_rsc
   vrDesc = QormasGift_desc_rsc

   viUse_type = ITEM_SINGLE_USE
   viUse_amount = 1
   viWeight = 90
   viBulk = 90


properties:  
   vrIcon = QormasGift_icon_rsc
   poRecipient = $

   pbMaskPossible = FALSE

messages:   

   Constructor(mask=FALSE)
   {
      Send(self,@SetRandomPaper);
      
      if (mask)
      {
         pbMaskPossible = TRUE;
      }
      
      propagate;
   }

   DestroyDisposable()
   {
      return;
   }
   
   GetRecipient()
   {
      return poRecipient;
   }
   
   SetRecipient(who=$)
   {
      poRecipient = who;
      
      return;
   }
   
   SetRandomPaper()
   {
      local iRandom;

      iRandom = Random(1,4);
      
      if iRandom = 1
      {
         vrIcon = QormasGift_icon_rsc1;
      }
      if iRandom = 2
      {
         vrIcon = QormasGift_icon_rsc2;
      }
      if iRandom = 3
      {
         vrIcon = QormasGift_icon_rsc3;
      }
      if iRandom = 4
      {
         vrIcon = QormasGift_icon_rsc4;
      }
      
      return;
   }

   ReqNewApply()
   {
      return TRUE;
   }

   NewApplied(what = $)
   {
      local rand;
      
      if (pbMaskPossible)
      {
         rand = Random(1,35);
      }
      else
      {
         rand = Random(1,24);
      }
      
      if rand >= 1 AND rand <=3
      {
         send(poOwner,@NewHold,#what=Create(&Scimitar));
      }
      if rand >= 4 AND rand <=6
      {
         send(poOwner,@NewHold,#what=Create(&InkyCap,#number=10));
      }
      if rand >= 7 AND rand <=9
      {
         send(poOwner,@NewHold,#what=Create(&DispellIllusionScroll));
         send(poOwner,@NewHold,#what=Create(&DispellIllusionScroll));
         send(poOwner,@NewHold,#what=Create(&DispellIllusionScroll));
      }
      if rand >= 10 AND rand <=12
      {
         send(poOwner,@NewHold,#what=Create(&IllusionaryFormPotion));
         send(poOwner,@NewHold,#what=Create(&IllusionaryFormPotion));
         send(poOwner,@NewHold,#what=Create(&IllusionaryFormPotion));
      }
      if rand >= 13 AND rand <=15
      {
         send(poOwner,@NewHold,#what=Create(&RescuePotion));
         send(poOwner,@NewHold,#what=Create(&RescuePotion));
         send(poOwner,@NewHold,#what=Create(&RescuePotion));
      }
      if rand >= 16 AND rand <=18
      {
         send(poOwner,@NewHold,#what=Create(&BonkStick));
      }
      if rand >= 19 AND rand <=21
      {
         send(poOwner,@NewHold,#what=Create(&SeduceWand));
         send(poOwner,@NewHold,#what=Create(&SeduceWand));
         send(poOwner,@NewHold,#what=Create(&SeduceWand));
      }
      if rand >= 22 AND rand <=24
      {
         send(poOwner,@NewHold,#what=Create(&OrcTooth,#number=10));
      }
      if rand = 25
      {
         send(poOwner,@NewHold,#what=Create(&XeoMask));
      }
      if rand = 26
      {
         send(poOwner,@NewHold,#what=Create(&DaemonMask));
      }
      if rand = 27
      {
         send(poOwner,@NewHold,#what=Create(&MummyMask));
      }
      if rand = 28
      {
         send(poOwner,@NewHold,#what=Create(&FeyMask));
      }
      if rand = 29
      {
         send(poOwner,@NewHold,#what=Create(&ShrunkenHeadMask));
      }
      if rand = 30
      {
         send(poOwner,@NewHold,#what=Create(&CowMask));
      }
      if rand = 31
      {
         send(poOwner,@NewHold,#what=Create(&SkullMask));
      }
      if rand = 32
      {
         send(poOwner,@NewHold,#what=Create(&RatMask));
      }
      if rand = 33
      {
         send(poOwner,@NewHold,#what=Create(&AntMask));
      }
      if rand = 34
      {
         send(poOwner,@NewHold,#what=Create(&TrollMask));
      }
      if rand = 35
      {
         send(poOwner,@NewHold,#what=Create(&KriipaMask));
      }
      
      Send(self,@Delete);
      
      return;
   }
   
   AppendDesc()
   {
      if poRecipient <> $
      {
         AppendTempString(QormasGift_desc_append1) ;
         AppendTempString(send(poRecipient,@GetTrueName));
         AppendTempString(QormasGift_desc_append2);
      }
      
      propagate;
   }
   
   NewOwner(what=$)
   {
      local oQormas;
      
      oQormas = Send(SYS,@GetQormas);
      
      if (oQormas <> $)
      {
         if (IsClass(what,&User))
         {
            Send(oQormas,@GiftClaimed,#what=self,#who=what);
         }
      }
   
      propagate;
   }
   
   ReqNewOwner(what = $)
   {
      if IsClass(what,&Room)
      {
         return TRUE;
      }
   
      if (what = poRecipient)
      {
         return TRUE;
      }
      
      return FALSE;
   }
   
   GetObjectFlags()
   {
      return LOOK_NO | GETTABLE_YES;
   }
   
end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

