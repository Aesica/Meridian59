% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
DamageRod is Rod

constants:

   include blakston.khd

resources:

   DamageRod_name_rsc = "rod of power"
   DamageRod_icon_rsc = wand5.bgf
   DamageRod_desc_rsc = "This rod is hot to the touch. Inscribed runes detail the process by which the magics "
                        "within enhance the attacks of the user for up to five seconds."
   DamageRod_success_rsc = "The rod flares in your hand, and you feel furious magics channel through you!"
   
   DamageRod_drop_rsc = "The offensive magics generated by your rod of power fade."
   DamageRod_success_wav = faren.wav

classvars:

   vrName = DamageRod_name_rsc
   vrIcon = DamageRod_icon_rsc

   viBulk = 25
   viWeight = 25
   viValue_average = 800

   viGround_group = 2
   viInventory_group = 1
   
   use_success_msg = DamageRod_success_rsc
   rod_success_wav_default_rsc = DamageRod_success_wav
   
   viStartHits = 3
   viStartMaxHits = 3
   viConsumesHits = 3

properties:
   
   vrDesc = DamageRod_desc_rsc
   
   pbActive = FALSE
   ptActiveTimer = $

messages:

   CastSpell()
   "Enhances owner's damage."
   {
      pbActive = TRUE;
      ptActiveTimer = CreateTimer(self,@DamageExpire,5000);

      return;
   }
   
   DamageExpire()
   {
      ptActiveTimer = $;
      pbActive = FALSE;
      
      if poOwner <> $
         AND IsClass(poOwner,&User)
         AND Send(poOwner,@IsLoggedOn)
      {
         Send(poOwner,@MsgSendUser,#message_rsc=DamageRod_drop_rsc);
      }
      
      return;
   }
   
   IsActive()
   {
      return pbActive;
   }
   
   Delete()
   {
      If ptActiveTimer <> $
      {
         DeleteTimer(ptActiveTimer);
         ptActiveTimer = $;
      }

      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
