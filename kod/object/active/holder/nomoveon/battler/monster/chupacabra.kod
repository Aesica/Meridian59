% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Chupacabra is Monster

constants:

   include blakston.khd

   ANIM_SPIKES = 2
   
resources:

   chupacabra_koc_name_rsc = "ixleocchupacabra"
   chupacabra_name_rsc = "chupacabra"
   chupacabra_icon_rsc = chupa.bgf
   chupacabra_desc_rsc = \
   "A chupacabra"

   chupacabra_dead_icon_rsc = chupa.bgf
   chupacabra_dead_name_rsc = "dead chupacabra"

   chupacabra_sound_hit = chupa_atkh.wav
   chupacabra_sound_miss = chupa_atkm.wav
   chupacabra_sound_death = chupa_dth.wav
   chupacabra_sound_aware = chupa_awr.wav

classvars:

   vrKocName = chupacabra_koc_name_rsc
   vrName = chupacabra_name_rsc
   vrIcon = chupacabra_icon_rsc
   vrDesc = chupacabra_desc_rsc
   vrDead_icon = chupacabra_dead_icon_rsc
   vrDead_name = chupacabra_dead_name_rsc

   viSpeed = SPEED_FAST
   viAttack_type = ATCK_WEAP_SLASH
   viDefault_behavior = AI_FIGHT_KARMA_AGGRESSIVE

   viAttributes = MOB_SPASM

   viDifficulty = 6
   viKarma = -75
   
   viCashmin = 250
   viCashmax = 750
   
   vrSound_hit = chupacabra_sound_hit
   vrSound_miss = chupacabra_sound_miss
   vrSound_aware = chupacabra_sound_aware
   vrSound_death = chupacabra_sound_death

properties:

   viLevel = 100
   viTreasure_type = TID_TOUGH

   piAnimation = ANIM_NONE

   vbSummoned = FALSE

   ptRevertControl = $

messages:

   Constructed()
   {
      plResistances = [ [-ATCK_SPELL_UNHOLY, 50 ],
                        [-ATCK_SPELL_HOLY, -25 ] ];
      propagate;
   }

   Constructor(DropsTreasure=TRUE,Level=0)
   {
      if Level > 0
      {
	 viLevel = Level;
      }
      if not DropsTreasure
      {
         viTreasure_type = TID_NONE;
         vbSummoned = TRUE;
      }
      propagate;
   }


   MonsterAttack(what = $)
   {
      piAnimation = ANIM_ATTACK;
      Send(poOwner,@SomethingChanged,#what=self);
      piAnimation = ANIM_NONE;
      
      return;
   }

   DoSpasm()
   {
      local roll;

      roll = Random(1,10);

      if roll <= 10
      {
         piAnimation = ANIM_SPIKES;
         Send(poOwner,@SomethingChanged,#what=self);
         piAnimation = ANIM_NONE;
      }

      propagate;
   }
   
   SendLookAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE);
      AddPacket(4,200,2,2,2,3);

      return;
   }

   SendMoveAnimation()
   {
      % Cycle through "walking" bitmaps (groups 2-5) at 50 milliseconds per frame
      AddPacket(1,ANIMATE_CYCLE,4,31,2,1,2,12);

      return;
   }

   SendAnimation()
   {
      if piAnimation = ANIM_ATTACK
      {
         AddPacket(1,ANIMATE_ONCE,4,28,2,13,2,18,2,1);
         return;
      }
      if piAnimation = ANIM_SPIKES
      {
         AddPacket(1,ANIMATE_ONCE,4,150,2,1,2,2,2,1);
      }
      % if no body animation
      
      propagate;
   }

   IsUndead()
   {
      return TRUE;
   }

   SetMaster(oMaster=$,DurationSecs=1)
   {
      if (oMaster <> $) and isClass(oMaster,&User)
      {
         ptRevertControl = createTimer(self,@RevertControl,(DurationSecs*1000));
      }
      propagate;
   }
   RevertControl()
   {
      ptRevertControl = $;
      send(self,@SetMaster,#oMaster=$);
      return;
   }
   

   Delete()
   {
      if ptRevertControl <> $
      {
         deleteTimer(ptRevertControl);
         ptRevertControl = $;
      }
      propagate;
   }
   
end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
