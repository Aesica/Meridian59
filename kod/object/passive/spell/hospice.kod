% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Hospice is Spell

constants:

   include blakston.khd

resources:

   hospice_name_rsc = "recuperate"
   hospice_icon_rsc = ihospice.bgf
   hospice_desc_rsc = \
      "Shal'ille's healing energy washes over you, allowing you to recover "
      "from serious injuries and resume battle once more! "
      "Requires three herbs to cast."

   hospice_cast_self = "Shal'ille's holy magic allows you to recuperate ~g~B%i~B~n hitpoints."

   hospice_unnecessary_rsc = "You are already perfectly healthy."

classvars:

   vrName = hospice_name_rsc
   vrIcon = hospice_icon_rsc
   vrDesc = hospice_desc_rsc

   viSchool = SS_SHALILLE
   viSpell_num = SID_HOSPICE
   viMana = 10
   viSpell_level = 3
   viChance_To_Increase = 15

   viCast_time = 0

   viFlash = FLASH_GOOD

properties:

   pbCan_cast_on_others = FALSE

messages:

   ResetReagents()
   {
      % separate message so can change, then set from admin mode

      plReagents = $;
      plReagents = Cons([&Herbs,3],plReagents);

      return;
   }

   GetNumSpellTargets()
   {
      return 0;
   }

   CanPayCosts(who = $)
   {
      if Send(who,@GetHealth) >= Send(who,@GetMaxHealth)
      {
         Send(who, @MsgSendUser, #message_rsc=hospice_unnecessary_rsc);
                
         return FALSE;
      }

      propagate;
   }

   CastSpell(who = $,iSpellPower = 0)
   {
      local iHeal;
      
      iHeal = random(-3,3) + (iSpellPower+1)/5 + Send(who,@GetKarma)/10;
      iheal = bound(iHeal,0,30);  

      Send(who,@MsgSendUser,#message_rsc=hospice_cast_self,#parm1=iHeal);

      Send(who,@GainHealthNormal,#amount=iHeal);

      propagate;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
